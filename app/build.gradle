apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

def final appConfig = [
        versions: [
                "support_library": "24.1.1",
                "kotlin": "$kotlin",
                "dagger": "2.5",
                "support_test": "0.5",
                "butternife": "8.2.1",
                "espresso": "2.2.2"
        ],

        manifestPlaceholder: [
                "fabricApiKey": rootProject.ext.fabricApiKey
        ],

        "base_url": "https://www.eslpod.com/website/show_all.php",
        "detail_url": "https://www.eslpod.com/website/show_podcast.php?issue_id=%s",
        "search_url": "https://cse.google.com/cse?cx=001507075980805982280%3Auhqfa0ga0ry&q=",
        "server_port": 8089
];

def generateVersionCode() {
    try {
        def cmd = 'git rev-list --count HEAD'
        cmd.execute().text.trim().toInteger()
    } catch (ignored) {
        1
    }
}

def generateVersionName() {
    try {
        def cmd = 'git describe --tags --abbrev=0'
        cmd.execute().text.trim()
    } catch (ignored) {
        "1.0"
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "br.com.wakim.eslpodclient"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        resConfigs "en", "pt-rBR"

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = appConfig.manifestPlaceholder

        resValue "string", "ad_settings_ads_unit_id", rootProject.ext.admobAppSettingsUnitId

        buildConfigField("String", "BASE_URL", "\"$appConfig.base_url\"")
        buildConfigField("String", "DETAIL_URL", "\"$appConfig.detail_url\"")
        buildConfigField("String", "SEARCH_URL", "\"$appConfig.search_url\"")
        buildConfigField("int", "SERVER_PORT", "$appConfig.server_port")
    }

    dexOptions {
        javaMaxHeapSize "2g"
        preDexLibraries true
        dexInProcess = true
    }

    signingConfigs {
        release {
            storeFile file("debug.keystore.jks")
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                if (!variant.buildType.isDebuggable()) {
                    def versionCode = generateVersionCode() ?: 1
                    def versionName = generateVersionName() + "." + versionCode ?: "1"

                    variant.mergedFlavor.versionCode = versionCode
                    variant.mergedFlavor.versionName = versionName
                }
            }
        }

        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    maven {
        url 'https://maven.fabric.io/public'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url  "http://dl.bintray.com/lukaville/maven"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':androidvideocache-library/library')

    compile "com.android.support:appcompat-v7:$appConfig.versions.support_library"
    compile "com.android.support:design:$appConfig.versions.support_library"
    compile "com.android.support:cardview-v7:$appConfig.versions.support_library"
    compile "com.android.support:recyclerview-v7:$appConfig.versions.support_library"
    compile "com.android.support:preference-v7:$appConfig.versions.support_library"
    compile "com.android.support:customtabs:$appConfig.versions.support_library"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$appConfig.versions.kotlin"
    compile 'org.jetbrains.anko:anko-sqlite:0.9'

    compile "com.google.dagger:dagger:$appConfig.versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$appConfig.versions.dagger"

    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.8'

    compile "com.jakewharton:butterknife:$appConfig.versions.butternife"
    kapt "com.jakewharton:butterknife-compiler:$appConfig.versions.butternife"

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'

    compile 'org.jsoup:jsoup:1.9.1'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.nononsenseapps:filepicker:3.0.0'

    compile 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:1.0.7'

    debugCompile 'com.facebook.stetho:stetho:1.3.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }

    compile 'com.google.firebase:firebase-ads:9.2.1'

    testCompile 'junit:junit:4.12'

    // Android Testing Support Library's runner and rules
    androidTestCompile ("com.android.support.test:runner:$appConfig.versions.support_test") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile ("com.android.support.test:rules:$appConfig.versions.support_test") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso UI Testing
    androidTestCompile ("com.android.support.test.espresso:espresso-core:$appConfig.versions.espresso") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$appConfig.versions.espresso") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

kapt {
    generateStubs = true
}

apply plugin: 'com.google.gms.google-services'